buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}

apply plugin: 'java'

group 'net.bndy'
version '2.0.0'

dependencies {
    compile project('lib-common')
    compile project('lib-data')
    compile project('lib-web')
    compile project('lib-mail')
}


subprojects {
    apply plugin: 'java-library'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.8 
    
    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_API_KEY')
        publications = ['maven']
        pkg {
            repo = 'Jlib'
            name = project.name
            userOrg = 'bndynet'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/bndynet/Jlib.git'
            websiteUrl = 'http://bndy.net'
            publicDownloadNumbers = true
            version {
                name = rootProject.version
                desc = project.name
                released = new Date()
                vcsTag = rootProject.version
            }
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'com.google.guava:guava:21.0'

        testImplementation 'junit:junit:4.12'
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }
    task copyDocs(type: Copy, dependsOn: javadocJar) {
        from 'build/docs'
        into "${rootProject.projectDir}/docs/${project.name}" 
    }
    javadocJar.finalizedBy(copyDocs)

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar, javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId rootProject.group
                artifactId project.name
                version rootProject.version
                from components.java
                
                customizePom(pom)
                // create the sign pom artifact
                pom.withXml {
                    def pomFile = file("${project.buildDir}/generated-pom.xml")
                    writeTo(pomFile)
                    def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                    artifact(pomAscFile) {
                        classifier = null
                        extension = 'pom.asc'
                    }
                }

                artifact tasks.sourcesJar
                artifact tasks.javadocJar
                
                // create the signed artifacts
                project.tasks.signArchives.signatureFiles.each {
                    artifact(it) {
                        def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                        if (matcher.find()) {
                            classifier = matcher.group(1)
                        } else {
                            classifier = null
                        }
                        extension = 'jar.asc'
                    }
                }
            }
        }
    }

   
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()
        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }
        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description project.name
            url 'https://github.com/bndynet/Jlib'

            organization {
                name 'BNDY-NET'
                url 'http://bndy.net'
            }

            scm {
                url 'https://github.com/bndynet/Jlib'
                connection 'scm:git:git://github.com/bndynet/Jlib.git'
                developerConnection 'scm:git:git@github.com:bndynet/Jlib.git'
            }

            licenses {
                license {
                    name 'The Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            developers {
                developer {
                    id 'bndynet'
                    name 'Bendy Zhang'
                    email 'zb@bndy.net'
                    url 'http://bndy.net'
                }
            }
        }
    }
}

